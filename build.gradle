plugins {
    id 'maven-publish'
    id 'fabric-loom' version '0.12-SNAPSHOT' apply false

    // https://github.com/Juuxel/LoomQuiltflower
    id 'io.github.juuxel.loom-quiltflower' version '1.7.0' apply false

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version 'b223d4a'
}

preprocess {
    def mc114 = createNode('1.14.4', 11404, 'yarn')
    def mc115 = createNode('1.15.2', 11502, 'yarn')
    def mc116 = createNode('1.16.5', 11605, 'yarn')
//    def mc117 = createNode('1.17.1', 11701, 'yarn')
//    def mc118 = createNode('1.18.2', 11802, 'yarn')

    mc115.link(mc114, null)
    mc115.link(mc116, null)
//    mc116.link(mc117, null)
//    mc117.link(mc118, null)
}

task buildAndGather {
    subprojects {
        dependsOn project.name + ':build'
    }
    doFirst {
        println 'Gathering builds'
        copy {
            subprojects {
                def libDir = project.projectDir.toPath().resolve('build/libs')
                from(libDir) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into 'build/libs/'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}
