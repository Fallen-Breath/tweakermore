plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

repositories {
	maven {
		url 'https://masa.dy.fi/maven'
	}
	maven {
		url 'https://maven.terraformersmc.com/releases'
	}
	maven {
		url 'https://www.cursemaven.com'
	}
	maven {
		url 'https://jitpack.io'
	}
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String versionSuffix = ''
if (System.getenv("BUILD_RELEASE") != "true") {
	versionSuffix += '-SHAPSHOT'
}
String buildNumber = System.getenv("BUILD_ID")
if (buildNumber != null) {
	versionSuffix += '+build.' + buildNumber
}
String fullModVersion = project.mod_version + versionSuffix

archivesBaseName = project.archives_base_name
version = 'mc' + project.minecraft_version + '-v' + fullModVersion
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// https://masa.dy.fi/maven/fi/dy/masa/malilib/
	modImplementation("fi.dy.masa.malilib:malilib-fabric-1.15.2:0.10.0-dev.21+arne.1") {
		exclude group: 'io.github.prospector', module: 'modmenu'
	}

	// https://www.curseforge.com/minecraft/mc-mods/tweakeroo/files
	modImplementation "curse.maven:tweakeroo-297344:2942277"  // tweakeroo-fabric-1.15.2-0.10.0-dev.20200424.222527.jar

	// https://www.curseforge.com/minecraft/mc-mods/item-scroller/files
	modImplementation "curse.maven:item-scroller-242064:2880360"  // itemscroller-fabric-1.15.2-0.15.0-dev.20200212.183513.jar

	// https://www.curseforge.com/minecraft/mc-mods/litematica/files
	modImplementation "curse.maven:litematica-308892:2957593"  // litematica-fabric-1.15.2-0.0.0-dev.20200515.184506.jar


	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	modImplementation "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"
	include "com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}"

	// runtime mods
	modRuntimeOnly "com.github.astei:lazydfu:0.1.2"  // faster startup
}

processResources {
	inputs.property "version", fullModVersion

	filesMatching("fabric.mod.json") {
		expand "version": fullModVersion
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
